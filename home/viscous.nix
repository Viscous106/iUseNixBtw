{ config, pkgs, inputs, ... }:

{
  imports = [
    inputs.hyprland.homeManagerModules.default
    ./modules/zsh.nix
    ./modules/neovim.nix
    ./modules/hyprland.nix
    ./modules/hypridle.nix
    ./modules/caelestia-quickshell.nix
    ./modules/extras.nix
    ./modules/git.nix
    ./modules/kitty.nix
    ./modules/zen.nix
    ./modules/gpg.nix
    ./modules/mpv.nix
    ./modules/btop.nix
    ./modules/yazi.nix
    ./modules/swaylock.nix
    ./modules/wlogout.nix
    ./modules/cava.nix
    ./modules/wl-kbptr.nix
    ./modules/thefuck.nix
    ./modules/qt5ct.nix
    ./modules/qt6ct.nix
    ./modules/gtk.nix
    ./modules/icon-themes.nix
    ./modules/wallpaper-picker.nix
    ./modules/nwg-look.nix
    ./modules/nwg-displays.nix
    ./modules/apps-browsers-comms.nix
    ./modules/apps-desktop-shell.nix
    ./modules/apps-media.nix
    ./modules/apps-misc.nix
    ./modules/dev-toolchains.nix
    ./modules/fonts-extra.nix
    ./modules/neovim-deps.nix
    ./modules/python-env.nix
    ./modules/hyprwhspr.nix
    ./modules/ydotoold.nix
    ./modules/battery-notify.nix
  ];

  home.username      = "viscous";
  home.homeDirectory = "/home/viscous";
  home.stateVersion  = "25.05";

  # ── PATH for systemd user session (Hyprland + shell subprocesses) ─────────
  # When Hyprland runs via systemd (systemd.enable = true), exec subprocesses
  # don't inherit the interactive-shell PATH. Explicitly include NixOS package
  # dirs so the shell's execs (the `caelestia` CLI invoked from keybinds,
  # playerctl, bash shebangs, etc.) resolve.
  home.sessionPath = [
    "/etc/profiles/per-user/${config.home.username}/bin"
    "/run/current-system/sw/bin"
  ];

  programs.home-manager.enable = true;
  # ── Symlink persisted secrets and data into $HOME at every login ──────────
  home.activation.linkSecrets = config.lib.dag.entryAfter [ "writeBoundary" ] ''
    # Ensure /persist/secrets exists or create a dummy structure for portability
    if [ ! -d /persist/secrets ]; then
       echo "Warning: /persist/secrets not found. Creating dummy structure for portability."
       $DRY_RUN_CMD mkdir -p /persist/secrets/ssh
       $DRY_RUN_CMD touch /persist/secrets/git-identity
       $DRY_RUN_CMD touch /persist/secrets/claude_api
       $DRY_RUN_CMD touch /persist/secrets/openai_api
       $DRY_RUN_CMD echo "# Add your git config here" > /persist/secrets/git-identity
       $DRY_RUN_CMD echo "# export CLAUDE_CODE_OAUTH_TOKEN=your_token_here" > /persist/secrets/claude_api
       $DRY_RUN_CMD echo "# export OPENAI_API_KEY=your_key_here" > /persist/secrets/openai_api
    fi

    # SSH keys and known_hosts
    if [ -d /persist/secrets/ssh ]; then
      $DRY_RUN_CMD mkdir -p $HOME/.ssh
      $DRY_RUN_CMD chmod 700 $HOME/.ssh
      [ -f /persist/secrets/ssh/id_ed25519 ]     && $DRY_RUN_CMD ln -sf /persist/secrets/ssh/id_ed25519     $HOME/.ssh/id_ed25519     || true
      [ -f /persist/secrets/ssh/id_ed25519.pub ] && $DRY_RUN_CMD ln -sf /persist/secrets/ssh/id_ed25519.pub $HOME/.ssh/id_ed25519.pub || true
      [ -f /persist/secrets/ssh/known_hosts ]   && $DRY_RUN_CMD ln -sf /persist/secrets/ssh/known_hosts   $HOME/.ssh/known_hosts   || true
    fi

    # Keyrings (GNOME Keyring data)
    $DRY_RUN_CMD mkdir -p $HOME/.local/share
    if [ -d /persist/home/viscous/.local/share/keyrings ]; then
      $DRY_RUN_CMD ln -sfn /persist/home/viscous/.local/share/keyrings $HOME/.local/share/keyrings || true
    fi
  '';

  # ── XDG dirs ──────────────────────────────────────────────────────────────
  # ── Extra user packages required by hypr scripts ─────────────────────────
  home.packages = with pkgs; [
    unstable.claude-code
    (pkgs.lib.lowPrio python3)  # redundant vs pythonWithLibs (home/modules/python-env.nix); kept for the plain interpreter, lowPrio to resolve bin/idle3.14 collision
    pkgs.ghgrab
    inputs.antigravity.packages.${pkgs.stdenv.hostPlatform.system}.google-antigravity-ide  # IDE (upstream renamed google-antigravity -> the 2.0 base app)
    inputs.antigravity.packages.${pkgs.stdenv.hostPlatform.system}.google-antigravity-cli  # `agy` terminal CLI
    inputs.codex-cli-nix.packages.${pkgs.stdenv.hostPlatform.system}.codex  # OpenAI Codex CLI (native binary, not the node wrapper)
    pkgs.codex-whoami  # which OpenAI org/project is Codex actually billing
    pkgs.claude-whoami # which Anthropic org/workspace is Claude Code using
    fd
    psmisc         # provides killall
    lsd           # better ls
    pyenv         # python version manager
    pulseaudio    # provides paplay
    bc            # math in shell scripts (Brightness, Volume, etc.)
    jq            # JSON parsing (WallpaperSelect, Weather, etc.)
    imagemagick   # image manipulation (WallpaperEffects)
    swappy        # screenshot annotation (ScreenShot.sh --swappy)
    wl-clipboard  # wl-copy / wl-paste (clipboard manager)
    cliphist      # clipboard history backend
    slurp         # region selection for screenshots
    grim          # screenshot tool
    awww          # wallpaper daemon (formerly swww)
    rofi          # app launcher (Super+D)
    swaynotificationcenter  # notification daemon (swaync CLI)
    # thunar / thunar-volman intentionally absent: they come from
    # programs.thunar in modules/desktop.nix, which is the only way the plugins
    # actually load. A second unwrapped copy here would shadow the wrapper.
    playerctl     # media controls
    pamixer       # volume control
    brightnessctl # brightness control
    pavucontrol   # audio GUI (Super+Alt+S)
    blueman       # bluetooth GUI (Super+Shift+B)
    tmux          # terminal multiplexer

    # Cursor
    bibata-cursors

    # From Nix profile
    antigen       # zsh plugin manager
    bat           # better cat
    cheese        # webcam app
    fastfetch     # system info
    gh            # github cli
    mysql84       # mysql 8.4 database
    hyprland-qtutils
    mpv           # media player
    nix-tree      # visualize nix dependencies
    wl-kbptr      # wayland keyboard pointer
    xev           # x11 event viewer
  ];

  xdg.enable = true;
  xdg.userDirs = {
    enable            = true;
    createDirectories = true;
    setSessionVariables = true;
  };

  # ── Pointer Cursor ────────────────────────────────────────────────────────
  home.pointerCursor = {
    enable  = true;   # implicit enable via this block is deprecated in home-manager
    package = pkgs.bibata-cursors;
    name    = "Bibata-Modern-Classic";
    size    = 20;
    gtk.enable = true;
    x11.enable = true;
  };
}
